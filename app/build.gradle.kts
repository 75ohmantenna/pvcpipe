/*
 * SPDX-FileCopyrightText: 2025 NewPipe e.V. <https://newpipe-ev.de>
 * SPDX-License-Identifier: GPL-3.0-or-later
 */

import com.android.build.api.dsl.ApplicationExtension

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.android.legacy.kapt)
    alias(libs.plugins.google.ksp)
    alias(libs.plugins.jetbrains.kotlin.parcelize)
    alias(libs.plugins.jetbrains.kotlinx.serialization)
    checkstyle
}


java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

kotlin {
    compilerOptions {
        // TODO: Drop annotation default target when it is stable
        freeCompilerArgs.addAll(
            "-Xannotation-default-target=param-property"
        )
    }
}

configure<ApplicationExtension> {
    compileSdk {
        version = release(37) {
            minorApiLevel = 0
        }
    }
    namespace = "org.schabi.newpipe"

    defaultConfig {
        applicationId = "org.seventyfiveohmantenna.pvcpipe"
        resValue("string", "app_name", "PVCPipe")
        minSdk = 26
        targetSdk = 37

        versionCode = (System.getProperty("versionCodeOverride")?.toInt() ?: 1013) + 580000

        versionName = "0.28.8-2.8.2"
        System.getProperty("versionNameSuffix")?.let { versionNameSuffix = it }

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            isDebuggable = true

            applicationIdSuffix = ".debug"
            resValue("string", "app_name", "PVCPipe Debug")
        }

        release {
            System.getProperty("packageSuffix")?.let { suffix ->
                applicationIdSuffix = suffix
                resValue("string", "app_name", "PVCPipe $suffix")
            }
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }


    lint {
        lintConfig = file("lint.xml")
        abortOnError = true
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        isCoreLibraryDesugaringEnabled = true
        encoding = "utf-8"
    }

    sourceSets {
        getByName("androidTest") {
            assets.directories += "$projectDir/schemas"
        }
    }

    androidResources {
        generateLocaleConfig = true
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
        resValues = true
    }

    packaging {
        resources {
            // remove two files which belong to jsoup
            // no idea how they ended up in the META-INF dir...
            excludes += setOf(
                "META-INF/README.md",
                "META-INF/CHANGES",
                "META-INF/COPYRIGHT" // "COPYRIGHT" belongs to RxJava...
            )
        }
    }
}

ksp {
    arg("room.schemaLocation", "$projectDir/schemas")
}


// Custom dependency configuration for ktlint
val ktlint = configurations.create("ktlint")

// https://checkstyle.org/#JRE_and_JDK
tasks.withType<Checkstyle>().configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

checkstyle {
    configDirectory = rootProject.file("checkstyle")
    isIgnoreFailures = false
    isShowViolations = true
    toolVersion = libs.versions.checkstyle.get()
}

tasks.register<Checkstyle>("runCheckstyle") {
    source("src")
    include("**/*.java")
    exclude("**/gen/**")
    exclude("**/R.java")
    exclude("**/BuildConfig.java")
    // Legacy downloader files with existing violations; new files are checked by default.
    exclude(
        "main/java/us/shandian/giga/get/DownloadInitializer.java",
        "main/java/us/shandian/giga/get/DownloadMission.java",
        "main/java/us/shandian/giga/get/DownloadMissionRecover.java",
        "main/java/us/shandian/giga/get/DownloadRunnable.java",
        "main/java/us/shandian/giga/get/DownloadRunnableFallback.java",
        "main/java/us/shandian/giga/get/Mission.java",
        "main/java/us/shandian/giga/get/sqlite/FinishedMissionStore.java",
        "main/java/us/shandian/giga/io/ChunkFileInputStream.java",
        "main/java/us/shandian/giga/io/CircularFileWriter.java",
        "main/java/us/shandian/giga/io/FileStream.java",
        "main/java/us/shandian/giga/io/FileStreamSAF.java",
        "main/java/us/shandian/giga/postprocessing/M4aNoDash.java",
        "main/java/us/shandian/giga/postprocessing/Mp4FromDashMuxer.java",
        "main/java/us/shandian/giga/postprocessing/OggFromWebmDemuxer.java",
        "main/java/us/shandian/giga/postprocessing/Postprocessing.java",
        "main/java/us/shandian/giga/postprocessing/PvcFromHlsRemuxer.java",
        "main/java/us/shandian/giga/postprocessing/TtmlConverter.java",
        "main/java/us/shandian/giga/postprocessing/WebMMuxer.java",
        "main/java/us/shandian/giga/preprocessing/PvcHlsPreProcessor.java",
        "main/java/us/shandian/giga/service/DownloadManager.java",
        "main/java/us/shandian/giga/service/DownloadManagerService.java",
        "main/java/us/shandian/giga/ui/adapter/MissionAdapter.java",
        "main/java/us/shandian/giga/ui/common/Deleter.java",
        "main/java/us/shandian/giga/ui/common/ProgressDrawable.java",
        "main/java/us/shandian/giga/ui/fragment/MissionsFragment.java",
        "main/java/us/shandian/giga/util/Utility.java",
    )

    classpath = configurations.getByName("checkstyle")

    isShowViolations = true

    reports {
        xml.required = true
        html.required = true
    }
}

val outputDir = project.layout.buildDirectory.dir("reports/ktlint/")
val inputFiles = fileTree("src") { include("**/*.kt") }

tasks.register<JavaExec>("runKtlint") {
    inputs.files(inputFiles)
    outputs.dir(outputDir)
    mainClass.set("com.pinterest.ktlint.Main")
    classpath = ktlint
    args = listOf("--editorconfig=../.editorconfig", "src/**/*.kt")
    jvmArgs = listOf("--add-opens", "java.base/java.lang=ALL-UNNAMED")
}

tasks.register<JavaExec>("formatKtlint") {
    inputs.files(inputFiles)
    outputs.dir(outputDir)
    mainClass.set("com.pinterest.ktlint.Main")
    classpath = ktlint
    args = listOf("--editorconfig=../.editorconfig", "-F", "src/**/*.kt")
    jvmArgs = listOf("--add-opens", "java.base/java.lang=ALL-UNNAMED")
}

tasks.register<CheckDependenciesOrder>("checkDependenciesOrder") {
    tomlFile = layout.projectDirectory.file("../gradle/libs.versions.toml")
}


dependencies {
    /** Desugaring **/
    coreLibraryDesugaring(libs.android.desugar)

    /** NewPipe libraries **/
    implementation(libs.newpipe.nanojson)
    implementation(libs.pvcpipe.extractor)
    implementation(libs.newpipe.filepicker)

    /** Checkstyle **/
    checkstyle(libs.puppycrawl.checkstyle)
    ktlint(libs.pinterest.ktlint)

    /** AndroidX **/
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.cardview)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.androidx.core)
    implementation(libs.androidx.documentfile)
    implementation(libs.androidx.fragment)
    implementation(libs.androidx.lifecycle.livedata)
    implementation(libs.androidx.lifecycle.viewmodel)
    implementation(libs.androidx.localbroadcastmanager)
    implementation(libs.androidx.media)
    implementation(libs.androidx.preference)
    implementation(libs.androidx.recyclerview)
    implementation(libs.androidx.room.runtime)
    implementation(libs.androidx.room.rxjava3)
    ksp(libs.androidx.room.compiler)
    implementation(libs.androidx.swiperefreshlayout)
    implementation(libs.androidx.viewpager2)
    implementation(libs.androidx.work.runtime)
    implementation(libs.androidx.work.rxjava3)
    implementation(libs.google.android.material)
    implementation(libs.androidx.webkit)

    // Coroutines interop
    implementation(libs.kotlinx.coroutines.rx3)

    // Kotlinx Serialization
    implementation(libs.kotlinx.serialization.json)

    /** Third-party libraries **/
    implementation(libs.livefront.bridge)
    implementation(libs.evernote.statesaver.core)
    kapt(libs.evernote.statesaver.compiler)

    // HTML parser
    implementation(libs.jsoup)

    // HTTP client
    implementation(libs.squareup.okhttp)

    // Media player
    implementation(libs.google.exoplayer.core)
    implementation(libs.google.exoplayer.dash)
    implementation(libs.google.exoplayer.database)
    implementation(libs.google.exoplayer.datasource)
    implementation(libs.google.exoplayer.hls)
    implementation(libs.google.exoplayer.mediasession)
    implementation(libs.google.exoplayer.smoothstreaming)
    implementation(libs.google.exoplayer.ui)

    // Manager for complex RecyclerView layouts
    implementation(libs.lisawray.groupie.core)
    implementation(libs.lisawray.groupie.viewbinding)

    // Image loading
    implementation(libs.coil.compose)
    implementation(libs.coil.network.okhttp)

    // Markdown library for Android
    implementation(libs.noties.markwon.core)
    implementation(libs.noties.markwon.linkify)

    // Crash reporting
    implementation(libs.acra.core)
    compileOnly(libs.google.autoservice.annotations)
    ksp(libs.zacsweers.autoservice.compiler)

    // Properly restarting
    implementation(libs.jakewharton.phoenix)

    // Reactive extensions for Java VM
    implementation(libs.reactivex.rxjava)
    implementation(libs.reactivex.rxandroid)
    // RxJava binding APIs for Android UI widgets
    implementation(libs.jakewharton.rxbinding)

    // Date and time formatting
    implementation(libs.ocpsoft.prettytime)

    /** Debugging **/
    // Memory leak detection
    debugImplementation(libs.squareup.leakcanary.watcher)
    debugImplementation(libs.squareup.leakcanary.plumber)
    debugImplementation(libs.squareup.leakcanary.core)
    // Debug bridge for Android
    debugImplementation(libs.facebook.stetho.core)
    debugImplementation(libs.facebook.stetho.okhttp3)

    /** Testing **/
    testImplementation(libs.junit)
    testImplementation(libs.mockito.core)

    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.runner)
    androidTestImplementation(libs.androidx.room.testing)
    androidTestImplementation(libs.assertj.core)
}

// keep the changed dependencies for PVCPipe more
// separate in hope of not getting to many merge conflicts
val okHttpVersion: String = libs.versions.okhttp.get()
// for JavaNetCookieJar see https://github.com/75ohmantenna/pvcpipe-extractor/issues/123
project.dependencies.implementation("com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion")
// for hls support on rumble
project.dependencies.implementation("com.github.evermind-zz:hlsdownloader:1.0.0")
project.dependencies.implementation(
    "com.github.75ohmantenna:slimhls-converter:7e7f373dbf",
)
// the eventbus
project.dependencies.implementation("org.greenrobot:eventbus:3.3.1")
// the LogcatToolkit
project.dependencies.implementation("com.github.evermind-zz:logcat-toolkit:1.0.0")
// cf challenge helper
project.dependencies.implementation("com.github.evermind-zz:challengeFloatsAway:1.1.1")
